{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP\n",
    "#using GLPKMathProgInterface\n",
    "using CPLEX"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#matriz de distancias aleatorias\n",
    "function doInstanciaAleatoria(n::Int64)\n",
    "    l = rand(1:1000, n*n)\n",
    "    for i in 1:n\n",
    "        l[(i-1)*n+i]=0\n",
    "    end\n",
    "    return reshape(l,n,n)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "n=25\n",
    "l=doInstanciaAleatoria(n)\n",
    "#n=5\n",
    "#l=[   0  967  894  192  168; 895    0  197  839  321; 25  992    0  712  651 ; 842  143   48    0  326 ; 905  449  787  654    0]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Based on https://github.com/JuliaOpt/JuMP.jl/blob/master/examples/old/tsp.jl\n",
    "\n",
    "# Both functions modify visited\n",
    "\n",
    "# extractTour reports a subtour\n",
    "function extractTour!(visited,n, initial,sol)\n",
    "    tour = [initial]  # Start at initial city\n",
    "    visited[initial]=true # Mark as visited \n",
    "    cur_city = initial\n",
    "    numVisited=0\n",
    "    while true\n",
    "        # Look for first arc out of current city\n",
    "        for j = 1:n\n",
    "            if sol[cur_city,j] >= 1-1e-6\n",
    "                # Found next city\n",
    "                cur_city=j\n",
    "                push!(tour, j)\n",
    "                # Mark as visited\n",
    "                visited[j]=true\n",
    "                numVisited += 1\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "        # If we have come back to initial city, stop\n",
    "        if cur_city == initial\n",
    "            break\n",
    "        end\n",
    "    end  # end while\n",
    "    return tour, numVisited\n",
    "end\n",
    "\n",
    "# findSubtour reports nodes in subtour\n",
    "function findInSubtour!(visited,n, initial, sol)\n",
    "    # Initialize to no subtour\n",
    "    subtour = fill(false,n)\n",
    "    # Always start looking at city 1\n",
    "    cur_city = initial\n",
    "    subtour[cur_city] = true\n",
    "    visited[cur_city] = true\n",
    "    subtour_length = 1\n",
    "    while true\n",
    "        # Find next node that we haven't yet visited\n",
    "        found_city = false\n",
    "        for j = 1:n\n",
    "            if !subtour[j]\n",
    "                if sol[cur_city, j] >= 1 - 1e-6\n",
    "                    # Arc to unvisited city, follow it\n",
    "                    cur_city = j\n",
    "                    subtour[j] = true\n",
    "                    visited[j]=true\n",
    "                    found_city = true\n",
    "                    subtour_length += 1\n",
    "                    break  # Move on to next city\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "        if !found_city\n",
    "            # We are done\n",
    "            break\n",
    "        end\n",
    "    end\n",
    "    return subtour, subtour_length\n",
    "end\n",
    "\n",
    "\n",
    "function solveATSP(l, n::Int64, s=GLPKSolverMIP(), eliminationConstraint=\"subtour\")\n",
    "    initialtime=time()\n",
    "    m = Model(solver = s)\n",
    "    \n",
    "    @variable(m, x[1:n,1:n], Bin)\n",
    "\n",
    "    @objective(m, Min, sum(l[i,j]*x[i,j] for i in 1:n for j in 1:n))\n",
    "    @constraint(m, outgoing[i=1:n],x[i,i] == 0)\n",
    "    @constraint(m, origin[i = 1:n], sum(x[i,1:n]) == 1)\n",
    "    @constraint(m, destination[j = 1:n], sum(x[1:n,j]) == 1)\n",
    "    \n",
    "    tt = 0.0\n",
    "    called = 0.0\n",
    "    separationtime=0.0\n",
    "    separated=0\n",
    "    typeConstraint=\"subtour\"\n",
    "    \n",
    "    function lazyGenerator(cb)\n",
    "        tt=time()\n",
    "        println(\"---- Inside subtour callback\")\n",
    "        inCircuit=fill(false,n)\n",
    "        for city in 1:n\n",
    "            if inCircuit[city]==0 \n",
    "                print(\"cut : \",city,\" \")\n",
    "                #two alternative cuts\n",
    "                # sum arcs in subtour <= |cities| -1\n",
    "                if eliminationConstraint == \"subtour\"\n",
    "                    subtour, subtour_length = extractTour!(inCircuit,n, city,getvalue(x))\n",
    "                end\n",
    "                # sum arcs from node in subtour to node not in subtour >= 2\n",
    "                if eliminationConstraint == \"cutset\"\n",
    "                    subtour, subtour_length = findInSubtour!(inCircuit,n,city,getvalue(x))\n",
    "                end\n",
    "                if subtour_length == n\n",
    "                    # This \"subtour\" is actually all cities, so we are done\n",
    "                    println(\"Solution visits all cities\")\n",
    "                    println(\"----\")\n",
    "                    return\n",
    "                end\n",
    "                println(\"with \",subtour_length,\" cities\")\n",
    "                if eliminationConstraint == \"subtour\"\n",
    "                    arcs_from_subtour = zero(AffExpr)\n",
    "                    for i =1:subtour_length\n",
    "                        arcs_from_subtour += x[subtour[i],subtour[i+1]]\n",
    "                    end\n",
    "                    @lazyconstraint(cb, arcs_from_subtour <= subtour_length-1)\n",
    "                    separated += 1\n",
    "                    separationtime += time()-tt\n",
    "                end\n",
    "                if eliminationConstraint == \"cutset\"\n",
    "                    arcs_from_subtour = zero(AffExpr)\n",
    "                    for i = 1:n\n",
    "                        if !subtour[i]\n",
    "                            # If this city isn't in subtour, skip it\n",
    "                            continue\n",
    "                        end\n",
    "                        # Want to include all arcs from this city, which is in\n",
    "                        # the subtour, to all cities not in the subtour\n",
    "                        for j = 1:n\n",
    "                            if i == j\n",
    "                                # Self-arc\n",
    "                                continue\n",
    "                            elseif subtour[j]\n",
    "                                # Both ends in same subtour\n",
    "                                continue\n",
    "                            else\n",
    "                                # j isn't in subtour\n",
    "                                arcs_from_subtour += x[i,j]\n",
    "                            end\n",
    "                        end\n",
    "                    end\n",
    "                    # Add the new subtour elimination constraint we built\n",
    "                    @lazyconstraint(cb, arcs_from_subtour >= 1)\n",
    "                    separated += 1\n",
    "                    separationtime += time()-tt\n",
    "                end\n",
    "                #println(arcs_from_subtour)\n",
    "            end\n",
    "        end\n",
    "        println(\"----\")\n",
    "    end\n",
    "    \n",
    "    addlazycallback(m, lazyGenerator) #es necesario un corte lazy ya que le indica al programa que no la he incluido\n",
    "    status = solve(m)\n",
    "    println(status)\n",
    "    println(\"Obj: \",getobjectivevalue(m))\n",
    "    println(\"Separated: \",separated)\n",
    "    println(\"Sep time: \",separationtime)\n",
    "    println(\"total time: \",time()-initialtime)\n",
    "    subtour, subtour_length = extractTour!(fill(false,n),n, 1,getvalue(x))\n",
    "    println(subtour)\n",
    "                    \n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "println(\"cutset\")\n",
    "#solveATSP(l,n,GLPKSolverMIP(),\"cutset\")\n",
    "solveATSP(l,n,CplexSolver(CPX_PARAM_SCRIND=0),\"cutset\")\n",
    "#println(\"subtour\")\n",
    "#solveATSP(l,n,GLPKSolverMIP(),\"subtour\")\n",
    "#solveATSP(l,n,CplexSolver(CPX_PARAM_SCRIND=0),\"subtour\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.2",
   "language": "julia",
   "name": "julia-0.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
